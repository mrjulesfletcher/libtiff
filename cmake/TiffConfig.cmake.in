include(CMakeFindDependencyMacro)

if(@ZLIB_SUPPORT@)
    find_dependency(ZLIB)
endif()
if(@LIBDEFLATE_SUPPORT@)
    find_dependency(Deflate)
endif()
if(@JPEG_SUPPORT@)
    find_dependency(JPEG)
endif()
if(@JBIG_SUPPORT@)
    find_dependency(JBIG)
endif()
if(@LERC_SUPPORT@)
    find_dependency(LERC)
endif()
if(@LZMA_SUPPORT@)
    find_dependency(liblzma)
endif()
if(@ZSTD_SUPPORT@)
    find_dependency(ZSTD)
endif()
if(@WEBP_SUPPORT@)
    find_dependency(WebP)
endif()
if(@TIFF_THREADPOOL@)
    find_dependency(Threads)
endif()

find_dependency(CMath)

function(set_variable_from_rel_or_absolute_path var root rel_or_abs_path)
    if(IS_ABSOLUTE "${rel_or_abs_path}")
        set(${var} "${rel_or_abs_path}" PARENT_SCOPE)
    else()
        set(${var} "${root}/${rel_or_abs_path}" PARENT_SCOPE)
    endif()
endfunction()

# Tell the user project where to find our headers and libraries
get_filename_component(_DIR "${CMAKE_CURRENT_LIST_DIR}/../../.." ABSOLUTE)
get_filename_component(_ROOT "${_DIR}/@PROJECT_ROOT_DIR@" ABSOLUTE)
# Use _ROOT as prefix here for the possibility of relocation after installation.
set_variable_from_rel_or_absolute_path("TIFF_INCLUDE_DIR" "${_ROOT}" "@CMAKE_INSTALL_INCLUDEDIR@")
set(TIFF_INCLUDE_DIRS ${TIFF_INCLUDE_DIR})

set(TIFF_LIBRARIES TIFF::tiff)

if(NOT TARGET TIFF::tiff)
    include("${CMAKE_CURRENT_LIST_DIR}/TiffTargets.cmake")
endif()

if(NOT "@BUILD_SHARED_LIBS@")
    if(@ZLIB_SUPPORT@)
        target_link_libraries(TIFF::tiff INTERFACE ZLIB::ZLIB)
    endif()
    if(@LIBDEFLATE_SUPPORT@)
        target_link_libraries(TIFF::tiff INTERFACE Deflate::Deflate)
    endif()
    if(@JPEG_SUPPORT@)
        target_link_libraries(TIFF::tiff INTERFACE JPEG::JPEG)
    endif()
    if(@JBIG_SUPPORT@)
        target_link_libraries(TIFF::tiff INTERFACE JBIG::JBIG)
    endif()
    if(@LERC_SUPPORT@)
        target_link_libraries(TIFF::tiff INTERFACE LERC::LERC)
    endif()
    if(@LZMA_SUPPORT@)
        target_link_libraries(TIFF::tiff INTERFACE liblzma::liblzma)
    endif()
    if(@ZSTD_SUPPORT@)
        target_link_libraries(TIFF::tiff INTERFACE ZSTD::ZSTD)
    endif()
    if(@WEBP_SUPPORT@)
        target_link_libraries(TIFF::tiff INTERFACE WebP::webp)
    endif()
    if(@TIFF_THREADPOOL@)
        target_link_libraries(TIFF::tiff INTERFACE Threads::Threads)
    endif()
    target_link_libraries(TIFF::tiff INTERFACE CMath::CMath)
endif()

unset (_ROOT)
unset (_DIR)
