include(${CMAKE_CURRENT_LIST_DIR}/TiffTestCommon.cmake)

get_filename_component(_src "${IMAGE}" ABSOLUTE)
if(NOT EXISTS "${_src}")
  if(DEFINED GENERATOR_PY AND DEFINED RAW2TIFF)
    message(STATUS "Generating image ${_src}")
    execute_process(COMMAND python3 "${GENERATOR_PY}" "${_src}" "${RAW2TIFF}" RESULT_VARIABLE _rv)
    if(_rv)
      message(FATAL_ERROR "Image generation failed")
    endif()
  else()
    message(FATAL_ERROR "Image ${_src} missing and no generator available")
  endif()
endif()

get_filename_component(_name "${_src}" NAME)
set(_dst "${OUTDIR}/${_name}")
if(NOT _src STREQUAL _dst)
  file(COPY "${_src}" DESTINATION "${OUTDIR}")
endif()

message(STATUS "Running ${ADDTIFFO} ${_dst}")
execute_process(COMMAND ${ADDTIFFO} ${_dst} RESULT_VARIABLE _rv)
if(_rv)
  message(FATAL_ERROR "addtiffo failed")
endif()

execute_process(COMMAND ${TIFFINFO} -D ${_dst} OUTPUT_VARIABLE _info RESULT_VARIABLE _rv)
if(_rv)
  message(FATAL_ERROR "tiffinfo failed")
endif()
string(REGEX MATCHALL "Directory" _matches "${_info}")
list(LENGTH _matches _count)
if(EXPECT_GT1)
  if(_count LESS 2)
    message(FATAL_ERROR "Expected more than one directory, got ${_count}")
  endif()
else()
  if(NOT _count EQUAL 1)
    message(FATAL_ERROR "Expected one directory, got ${_count}")
  endif()
endif()

